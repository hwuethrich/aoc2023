# Day 6: Wait For It

```elixir
Mix.install([
  {:smaoc, git: "https://github.com/nettinho/smaoc"}
])
```

## Part 1

The ferry quickly brings you across Island Island. After asking around, you discover that there is indeed normally a large pile of sand somewhere near here, but you don't see anything besides lots of water and the small island where the ferry has docked.

As you try to figure out what to do next, you notice a poster on a wall near the ferry dock. "Boat races! Open to the public! Grand prize is an all-expenses-paid trip to Desert Island!" That must be where the sand comes from! Best of all, the boat races are starting in just a few minutes.

You manage to sign up as a competitor in the boat races just in time. The organizer explains that it's not really a traditional race - instead, you will get a fixed amount of time during which your boat has to travel as far as it can, and you win if your boat goes the farthest.

As part of signing up, you get a sheet of paper (your puzzle input) that lists the time allowed for each race and also the best distance ever recorded in that race. To guarantee you win the grand prize, you need to make sure you go farther in each race than the current record holder.

The organizer brings you over to the area where the boat races are held. The boats are much smaller than you expected - they're actually toy boats, each with a big button on top. Holding down the button charges the boat, and releasing the button allows the boat to move. Boats move faster if their button was held longer, but time spent holding the button counts against the total race time. You can only hold the button at the start of the race, and boats don't move until the button is released.

For example:

```
Time:      7  15   30
Distance:  9  40  200
```

This document describes three races:

The first race lasts 7 milliseconds. The record distance in this race is 9 millimeters.
The second race lasts 15 milliseconds. The record distance in this race is 40 millimeters.
The third race lasts 30 milliseconds. The record distance in this race is 200 millimeters.
Your toy boat has a starting speed of zero millimeters per millisecond. For each whole millisecond you spend at the beginning of the race holding down the button, the boat's speed increases by one millimeter per millisecond.

So, because the first race lasts 7 milliseconds, you only have a few options:

* Don't hold the button at all (that is, hold it for 0 milliseconds) at the start of the race. The boat won't move; it will have traveled 0 millimeters by the end of the race.
* Hold the button for 1 millisecond at the start of the race. Then, the boat will travel at a speed of 1 millimeter per millisecond for 6 milliseconds, reaching a total distance traveled of 6 millimeters.
* Hold the button for 2 milliseconds, giving the boat a speed of 2 millimeters per millisecond. It will then get 5 milliseconds to move, reaching a total distance of 10 millimeters.
* Hold the button for 3 milliseconds. After its remaining 4 milliseconds of travel time, the boat will have gone 12 millimeters.
* Hold the button for 4 milliseconds. After its remaining 3 milliseconds of travel time, the boat will have gone 12 millimeters.
* Hold the button for 5 milliseconds, causing the boat to travel a total of 10 millimeters.
* Hold the button for 6 milliseconds, causing the boat to travel a total of 6 millimeters.
* Hold the button for 7 milliseconds. That's the entire duration of the race. You never let go of the button. The boat can't move until you let go of the button. Please make sure you let go of the button so the boat gets to move. 0 millimeters.

Since the current record for this race is 9 millimeters, there are actually 4 different ways you could win: you could hold the button for 2, 3, 4, or 5 milliseconds at the start of the race.

In the second race, you could hold the button for at least 4 milliseconds and at most 11 milliseconds and beat the record, a total of 8 different ways to win.

In the third race, you could hold the button for at least 11 milliseconds and no more than 19 milliseconds and still beat the record, a total of 9 ways you could win.

To see how much margin of error you have, determine the number of ways you can beat the record in each race; in this example, if you multiply these values together, you get 288 (4 * 8 * 9).

Determine the number of ways you could beat the record in each race. What do you get if you multiply these numbers together?

## Part 2

As the race is about to start, you realize the piece of paper with race times and record distances you got earlier actually just has very badkerning. There's really only one race - ignore the spaces between the numbers on each line.

So, the example from before:

```
Time:      7  15   30
Distance:  9  40  200
```

...now instead means this:

```
Time:      71530
Distance:  940200
```

Now, you have to figure out how many ways there are to win this single race. In this example, the race lasts for 71530 milliseconds and the record distance you need to beat is 940200 millimeters. You could hold the button anywhere from 14 to 71516 milliseconds and beat the record, a total of 71503 ways!

How many ways can you beat the record in this one much longer race?

## Solution

<!-- livebook:{"attrs":{"day":"6","example_input":"Time:      7  15   30\nDistance:  9  40  200\n","input":"Time:        53     89     76     98\nDistance:   313   1090   1214   1201\n","run_config":["puzzle","part2"],"source":"defmodule Smaoc.Solution do\n\n  def solve(:part1, input) do\n    input \n    |> parse_races()\n    |> Enum.map(&time_options/1)\n    |> Enum.product()\n  end\n\n  def solve(:part2, input) do\n    input \n    |> parse_race()\n    |> time_options()\n  end\n\n  defp time_options(race) do\n    {t1, t2} = time_bounds(race)\n    Float.ceil(t2) - Float.floor(t1) - 1\n  end\n\n  defp time_bounds({time, dist} = _race) do\n    a = time / 2\n    b = :math.sqrt(-4 * dist + :math.pow(time, 2)) / 2\n    {a - b, a + b}\n  end\n\n  defp parse_lines(input) do\n    input\n    |> String.split(\"\\n\")\n    |> Enum.reject(&(&1 == \"\"))\n  end\n\n  ### Part 1\n\n  defp parse_races(input) do\n    input\n    |> parse_lines()\n    |> Enum.map(&parse_numbers/1)\n    |> Enum.zip()\n  end\n\n  defp parse_numbers(line) do\n    [_, numbers] = String.split(line, \":\")\n    numbers |> String.split(\" \", trim: true) |> Enum.map(&String.to_integer/1)\n  end\n\n  ### Part 2\n\n  defp parse_race(input) do\n    input\n    |> parse_lines()\n    |> Enum.map(&parse_number/1)\n    |> List.to_tuple()\n  end\n\n  defp parse_number(line) do\n    [_, number] = String.split(line, \":\")\n    number |> String.replace(\" \", \"\") |> String.to_integer()\n  end\nend","year":"2023"},"chunks":null,"kind":"Elixir.Smaoc.SmartCell","livebook_object":"smart_cell"} -->

```elixir
defmodule Smaoc.Solution do
  def solve(:part1, input) do
    input
    |> parse_races()
    |> Enum.map(&time_options/1)
    |> Enum.product()
  end

  def solve(:part2, input) do
    input
    |> parse_race()
    |> time_options()
  end

  defp time_options(race) do
    {t1, t2} = time_bounds(race)
    Float.ceil(t2) - Float.floor(t1) - 1
  end

  defp time_bounds({time, dist} = _race) do
    a = time / 2
    b = :math.sqrt(-4 * dist + :math.pow(time, 2)) / 2
    {a - b, a + b}
  end

  defp parse_lines(input) do
    input
    |> String.split("\n")
    |> Enum.reject(&(&1 == ""))
  end

  ### Part 1

  defp parse_races(input) do
    input
    |> parse_lines()
    |> Enum.map(&parse_numbers/1)
    |> Enum.zip()
  end

  defp parse_numbers(line) do
    [_, numbers] = String.split(line, ":")
    numbers |> String.split(" ", trim: true) |> Enum.map(&String.to_integer/1)
  end

  ### Part 2

  defp parse_race(input) do
    input
    |> parse_lines()
    |> Enum.map(&parse_number/1)
    |> List.to_tuple()
  end

  defp parse_number(line) do
    [_, number] = String.split(line, ":")
    number |> String.replace(" ", "") |> String.to_integer()
  end
end

year = "2023"
day = "6"
run_config = ["puzzle", "part2"]

inputs = %{
  "puzzle" => "Time:        53     89     76     98\nDistance:   313   1090   1214   1201\n",
  "example" => "Time:      7  15   30\nDistance:  9  40  200\n"
}

response =
  for part <- ["part1", "part2"], input_key <- ["puzzle", "example"] do
    {part, input_key}
  end

response =
  response
  |> Enum.filter(fn {part, input_key} ->
    part in run_config and input_key in run_config
  end)
  |> Enum.map(fn {part, input_key} ->
    [part, input_key, Smaoc.Solution.solve(String.to_atom(part), inputs[input_key])]
  end)

Smaoc.Response.new(%{response: response, run_config: run_config, year: year, day: day})
```

<!-- livebook:{"offset":8719,"stamp":{"token":"XCP.hyR6QMJnosIDUJHLfXT0eFtIa9D_xRJk4OMt90fHrYRiRFx3nSnGkf_TNXw3yajEjxn4v41Fn1CjoA97ha8MyPWgdstzL323HAy5qO3FLuwHl8YXWvk","version":2}} -->
